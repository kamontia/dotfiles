# Compile zshrc if modified
if [[ ~/.zshrc -nt ~/.zshrc.zwc ]]; then
  zcompile ~/.zshrc
fi

### Environment variables
export PATH="$HOME/.local/bin:$PATH"
export BUN_INSTALL="$HOME/.bun"
export PATH="$BUN_INSTALL/bin:$PATH"
export PATH="$HOME/.antigravity/antigravity/bin:$PATH"

### Speed up init by caching eval outputs
mkdir -p ~/.cache
for tool in starship zoxide; do
  cache_file=~/.cache/${tool}_init.zsh
  if [[ ! -f $cache_file.zwc ]] || [[ "$(command -v $tool)" -nt $cache_file.zwc ]]; then
    if [[ $tool == "zoxide" ]]; then
      $tool init zsh --cmd j > $cache_file
    else
      $tool init zsh > $cache_file 2>/dev/null
    fi
    zcompile $cache_file
  fi
  source $cache_file
done
unset cache_file

# zoxide add runs synchronously (~95ms). Override to run in background.
function __zoxide_hook() {
  \command zoxide add -- "$(__zoxide_pwd)" &!
}

### mise: no hooks, just apply environment once at startup
# Hooks are disabled for speed. Use `mise shell` to switch versions manually.
# Cache the hook-env output so tools (node, go, etc.) are on PATH immediately.
{
  local mise_env_cache=~/.cache/mise_hook_env.zsh
  if [[ ! -f $mise_env_cache ]] || [[ "$(command -v mise)" -nt $mise_env_cache ]]; then
    /Users/kamohara/.local/bin/mise hook-env -s zsh > $mise_env_cache 2>/dev/null
  fi
  [[ -s $mise_env_cache ]] && source $mise_env_cache
}

### For Mac
typeset -U path PATH
path=( /opt/homebrew/bin(N-/) /usr/local/bin(N-/) $path )

### Aliases
if (( $+commands[eza] )); then
  alias e='eza --icons --git'
  alias l='clear && eza --icons --git'
  alias ls='eza --icons --git'
  alias ea='eza -a --icons --git'
  alias la=ea
  alias ee='eza -aahl --icons --git'
  alias ll=ee
  alias et='eza -T -L 3 -a -I "node_modules|.git|.cache" --icons'
  alias lt=et
  alias eta='eza -T -a -I "node_modules|.git|.cache" --color=always --icons | less -r'
  alias lta=eta
fi
alias y='yazi'

### Claude Code: bedrock by default, "claude o" for OAuth
function claude() {
  if [[ "$1" == "o" ]]; then
    shift
    command claude "$@"
  else
    CLAUDE_CODE_USE_BEDROCK=true \
    AWS_PROFILE=dev \
    AWS_REGION=ap-northeast-1 \
    ANTHROPIC_MODEL="arn:aws:bedrock:ap-northeast-1:339712865033:application-inference-profile/47mqf5tqsu9g" \
    command claude "$@"
  fi
}

### ghq + fzf
function ghq-fzf() {
  local src=$(ghq list -p | fzf --layout=reverse --preview "bat --color=always --style=header,grid --line-range :80 {}/(README|readme).*")
  if [[ -n "$src" ]]; then
    BUFFER="cd $src"
    zle accept-line
  fi
  zle -R -c
}
zle -N ghq-fzf

### ZSH Vi Mode Settings (Define BEFORE loading plugin)
export ZVM_VI_INSERT_ESCAPE_BINDKEY="jk"
export ZVM_KEYTIMEOUT=0.3
export STARSHIP_VI_MODE="INSERT"

# Keybindings hook
function zvm_after_lazy_keybindings() {
  # Use native bindkey if zvm_define_keymap is missing
  bindkey -M viins '^]' ghq-fzf
  bindkey -M vicmd '^]' ghq-fzf
}

# Mode selection hook
function zvm_after_select_vi_mode() {
  case $ZVM_MODE in
    $ZVM_MODE_NORMAL)  export STARSHIP_VI_MODE="NORMAL" ;;
    $ZVM_MODE_INSERT)  export STARSHIP_VI_MODE="INSERT" ;;
    $ZVM_MODE_VISUAL)  export STARSHIP_VI_MODE="VISUAL" ;;
    $ZVM_MODE_REPLACE) export STARSHIP_VI_MODE="REPLACE" ;;
  esac
  zle reset-prompt
}

### Zinit (Plugin Manager)
ZINIT_HOME="$HOME/.local/share/zinit/zinit.git"
if [[ ! -f $ZINIT_HOME/zinit.zsh ]]; then
    command mkdir -p "$HOME/.local/share/zinit" && command chmod g-rwX "$HOME/.local/share/zinit"
    command git clone https://github.com/zdharma-continuum/zinit "$ZINIT_HOME"
fi
source "$ZINIT_HOME/zinit.zsh"
autoload -Uz _zinit
(( ${+_comps} )) && _comps[zinit]=_zinit

zstyle ':zinit:*' banner 'no'

# Load ZSH Vi Mode
zinit light jeffreytse/zsh-vi-mode

# Other plugins with wait
zinit ice wait'0' lucid; zinit load zdharma-continuum/history-search-multi-word
zinit ice wait'0' lucid atload'_zsh_autosuggest_start'; zinit light zsh-users/zsh-autosuggestions
zinit ice wait'0' lucid atinit'zpcompinit; zpcdreplay'; zinit light zdharma-continuum/fast-syntax-highlighting

# FZF, Bun, GCloud snippets
zinit ice wait'0' lucid; zinit snippet /opt/homebrew/opt/fzf/shell/completion.zsh
zinit ice wait'0' lucid; zinit snippet /opt/homebrew/opt/fzf/shell/key-bindings.zsh
[[ -s "$HOME/.bun/_bun" ]] && { zinit ice wait'0' lucid; zinit snippet "$HOME/.bun/_bun" }
[[ -f "$HOME/google-cloud-sdk/path.zsh.inc" ]] && { zinit ice wait'0' lucid; zinit snippet "$HOME/google-cloud-sdk/path.zsh.inc" }
[[ -f "$HOME/google-cloud-sdk/completion.zsh.inc" ]] && { zinit ice wait'1' lucid; zinit snippet "$HOME/google-cloud-sdk/completion.zsh.inc" }

### Completions (compinit is handled by zinit's zpcompinit above)
fpath=($HOME/.docker/completions $fpath)

# Final re-bind just in case
bindkey -M viins '^]' ghq-fzf
bindkey -M vicmd '^]' ghq-fzf

### MANAGED BY RANCHER DESKTOP START (DO NOT EDIT)
export PATH="/Users/kamohara/.rd/bin:$PATH"
### MANAGED BY RANCHER DESKTOP END (DO NOT EDIT)
