# Compile zshrc if modified
if [[ ~/.zshrc -nt ~/.zshrc.zwc ]]; then
  zcompile ~/.zshrc
fi

### Environment variables
export PATH="/Users/kamo/.local/bin:$PATH"
export BUN_INSTALL="$HOME/.bun"
export PATH="$BUN_INSTALL/bin:$PATH"
export PATH="/Users/kamo/.antigravity/antigravity/bin:$PATH"

### Speed up init by caching eval outputs
mkdir -p ~/.cache
for tool in starship mise zoxide; do
  if [[ ! -f ~/.cache/${tool}_init.zsh || "$(command -v $tool)" -nt ~/.cache/${tool}_init.zsh ]]; then
    $tool init zsh > ~/.cache/${tool}_init.zsh 2>/dev/null || $tool activate zsh > ~/.cache/${tool}_init.zsh
    zcompile ~/.cache/${tool}_init.zsh
  fi
  source ~/.cache/${tool}_init.zsh
done

### For Mac
typeset -U path PATH
path=( /opt/homebrew/bin(N-/) /usr/local/bin(N-/) $path )

### Aliases
if (( $+commands[exa] )); then
  alias e='exa --icons --git'
  alias l='clear && exa --icons --git'
  alias ls='exa --icons --git'
  alias ea='exa -a --icons --git'
  alias la=ea
  alias ee='exa -aahl --icons --git'
  alias ll=ee
  alias et='exa -T -L 3 -a -I "node_modules|.git|.cache" --icons'
  alias lt=et
  alias eta='exa -T -a -I "node_modules|.git|.cache" --color=always --icons | less -r'
  alias lta=eta
fi
alias g="env"
alias y='yazi'

### ghq + fzf
function ghq-fzf() {
  local src=$(ghq list -p | fzf --layout=reverse --preview "bat --color=always --style=header,grid --line-range :80 {}/(README|readme).*")
  if [[ -n "$src" ]]; then
    BUFFER="cd $src"
    zle accept-line
  fi
  zle -R -c
}
zle -N ghq-fzf

### ZSH Vi Mode Settings (Define BEFORE loading plugin)
export ZVM_VI_INSERT_ESCAPE_BINDKEY="jk"
export ZVM_KEYTIMEOUT=0.3
export STARSHIP_VI_MODE="INSERT"

# Keybindings hook
function zvm_after_lazy_keybindings() {
  # Use native bindkey if zvm_define_keymap is missing
  bindkey -M viins '^]' ghq-fzf
  bindkey -M vicmd '^]' ghq-fzf
}

# Mode selection hook
function zvm_after_select_vi_mode() {
  case $ZVM_MODE in
    $ZVM_MODE_NORMAL)  export STARSHIP_VI_MODE="NORMAL" ;;
    $ZVM_MODE_INSERT)  export STARSHIP_VI_MODE="INSERT" ;;
    $ZVM_MODE_VISUAL)  export STARSHIP_VI_MODE="VISUAL" ;;
    $ZVM_MODE_REPLACE) export STARSHIP_VI_MODE="REPLACE" ;;
  esac
  zle reset-prompt
}

### Zinit (Plugin Manager)
ZINIT_HOME="$HOME/.local/share/zinit/zinit.git"
if [[ ! -f $ZINIT_HOME/zinit.zsh ]]; then
    command mkdir -p "$HOME/.local/share/zinit" && command chmod g-rwX "$HOME/.local/share/zinit"
    command git clone https://github.com/zdharma-continuum/zinit "$ZINIT_HOME"
fi
source "$ZINIT_HOME/zinit.zsh"
autoload -Uz _zinit
(( ${+_comps} )) && _comps[zinit]=_zinit

zstyle ':zinit:*' banner 'no'

# Load ZSH Vi Mode
zinit light jeffreytse/zsh-vi-mode

# Other plugins with wait
zinit ice wait'0' lucid; zinit load zdharma-continuum/history-search-multi-word
zinit ice wait'0' lucid atload'_zsh_autosuggest_start'; zinit light zsh-users/zsh-autosuggestions
zinit ice wait'0' lucid atinit'zpcompinit; zpcdreplay'; zinit light zdharma-continuum/fast-syntax-highlighting

# FZF, Bun, GCloud snippets
zinit ice wait'0' lucid; zinit snippet /Users/kamo/.fzf/shell/completion.zsh
zinit ice wait'0' lucid; zinit snippet /Users/kamo/.fzf/shell/key-bindings.zsh
[[ -s "$HOME/.bun/_bun" ]] && { zinit ice wait'0' lucid; zinit snippet "$HOME/.bun/_bun" }
[[ -f '/Users/kamo/google-cloud-sdk/path.zsh.inc' ]] && { zinit ice wait'0' lucid; zinit snippet '/Users/kamo/google-cloud-sdk/path.zsh.inc' }
[[ -f '/Users/kamo/google-cloud-sdk/completion.zsh.inc' ]] && { zinit ice wait'1' lucid; zinit snippet '/Users/kamo/google-cloud-sdk/completion.zsh.inc' }

### Completions & compinit
fpath=(/Users/kamo/.docker/completions $fpath)
autoload -Uz compinit
[[ -n ${ZDOTDIR:-$HOME}/.zcompdump(#qN.m-1) ]] && compinit -C || compinit

# Final re-bind just in case
bindkey -M viins '^]' ghq-fzf
bindkey -M vicmd '^]' ghq-fzf